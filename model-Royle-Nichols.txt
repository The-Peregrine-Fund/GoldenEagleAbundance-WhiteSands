
    model {
    # p.beta: prob. of detection
    # lam.beta: abundance (log-link scale, use exp function to backtransform)
    ##### PRIORS ###############################################

    lp.beta <- logit(p.beta)
    p.beta ~ dbeta(1,1)
    lam.beta ~ dnorm(0, 0.01)
    beta1 ~ dnorm(0, 0.01)
    beta2 ~ dnorm(0, 0.01)
    sd.year ~ dnorm(0, 1/(2*2))T(0,) # translates tau to sd=2
    sd.site ~ dnorm(0, 1/(2*2))T(0,)
    sd.both ~ dnorm(0, 1/(2*2))T(0,)
    
    # Royle Nichols model
    # observation model
      for (i in 1:ndata) {
        for (j in 1:nvisits) {
          y[i,j] ~ dbern(Pstar[i,j])
          Pstar[i,j] <- 1-(1-p[i,j])^N[i]
          logit(p[i,j]) <- lp.beta + 
                            beta1*num_surveyors[i,j] +
                            beta2*oday.sc[i,j]
    } # j
    # abundance model
        N[i] ~ dpois(lambda[i])
        log(lambda[i]) <- lam.beta + 
                          eps[ year[i] ] +
                          eta[ site[i] ] +
                          nu[ year[i], site[i]]
    } # i
    # random effect for year
    for (t in 1:nyears){
    eps[t] ~ dnorm(0, sd.year)
    }
    for (s in 1:nsites){
    eta[s] ~ dnorm(0, sd.site)
        for (t in 1:nyears){
          nu[ t, s] ~ dnorm(0, sd.both)
    }}
    } # End model
    